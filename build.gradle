plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'at.petra-k.pkpcpbp'
version = properties["pluginVersion"] + "-pre-" + System.getenv("BUILD_NUMBER")

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation gradleApi()

    implementation("blue.endless:jankson:1.2.2")
    implementation group: 'com.diluv.schoomp', name: 'Schoomp', version: '1.2.6'
}

gradlePlugin {
    // Define the plugin
    plugins {
        pkpcpbp {
            id = 'at.petra-k.PKPlugin'
            displayName = 'P.K.P.C.P.B.P.'
            description = "Petra Kat's Pretty Cool Publishing Boilerplate Plugin"
            implementationClass = 'at.petrak.pkpcpbp.PKPlugin'
        }
        pkpcpbpSubproj {
            id = 'at.petra-k.PKSubprojPlugin'
            displayName = 'PKPCPBP (Subprojs)'
            description = "Subprojects PKPCPBP"
            implementationClass = 'at.petrak.pkpcpbp.PKSubprojPlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
